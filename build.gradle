plugins {
  id 'maven-publish'
  id 'fabric-loom' version '1.3-SNAPSHOT' apply false
  id 'com.replaymod.preprocess' version '20c7ec554a'
}

preprocess {
  // def mc1165 = createNode('1.16.5', 1_16_05, 'yarn')
  def mc1171 = createNode('1.17.1', 1_17_01, 'yarn')
  def mc1182 = createNode('1.18.2', 1_18_02, 'yarn')
  def mc1193 = createNode('1.19.3', 1_19_03, 'yarn')
  def mc1194 = createNode('1.19.4', 1_19_04, 'yarn')
  def mc1201 = createNode('1.20.1', 1_20_01, 'yarn')

  // mc1165.link(mc1171, file('versions/mapping-1.16-1.17.txt'))
  mc1171.link(mc1182, null)
  mc1182.link(mc1193, null)
  mc1193.link(mc1194, null)
  mc1194.link(mc1201, file('versions/mapping-1.19.4-1.20.1.txt'))
}

tasks.register('buildAndGather') {
  subprojects {
    dependsOn project.tasks.named('build').get()
  }
  doFirst {
    println 'Gathering builds'
    def buildLibs = {
      p -> p.buildDir.toPath().resolve('libs')
    }
    delete fileTree(buildLibs(rootProject)) {
      include '*'
    }
    subprojects {
      copy {
        from(buildLibs(project)) {
          include '*.jar'
          exclude '*-dev.gar', '*-sources.jar'
        }
        into buildLibs(rootProject)
        duplicatesStrategy duplicatesStrategy.INCLUDE
      }
    }
  }
}
